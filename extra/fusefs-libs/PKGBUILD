# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=fusefs-libs
pkgver=2.9.2
pkgrel=1
pkgdesc="allows filesystem implementation in userspace"
arch=('i686' 'x86_64')
url="http://fuse4bsd.creo.hu/"
license=('BSD')
groups=('fuse')
source=(http://downloads.sourceforge.net/fuse/fuse-${pkgver}.tar.gz
        patch-lib_helper.c
        patch-lib_mount_bsd.c
        patch-lib_mount_util.c
        patch-lib_mount_util.h
        )
md5sums=('7d80d0dc9cc2b9199a0c53787c151205'
         '6747fafbc85bcb17f74ae6b869a457e8'
         '87f960cbc2ee8e74b96d2937dd03065f'
         'e3b94121dd9ae4e1f9aa80d17f87e803'
         'f6f1a49bf3f9b28f7eafd6c0b1f8ab9c')

build() {
    cd "${srcdir}/fuse-${pkgver}"
    patch -p0 -i ${srcdir}/patch-lib_helper.c
    patch -p0 -i ${srcdir}/patch-lib_mount_bsd.c
    patch -p0 -i ${srcdir}/patch-lib_mount_util.c
    patch -p0 -i ${srcdir}/patch-lib_mount_util.h

    sed -i '' -e 's,-lpthread,${PTHREAD_LIBS},' \
        -e 's,-D_FILE_OFFSET_BITS=64,-D_FILE_OFFSET_BITS=64 ${PTHREAD_CFLAGS},' \
        fuse.pc.in
    sed -i '' -e 's,-lpthread,${PTHREAD_LIBS},' \
        lib/Makefile.in \
        example/Makefile.in

    ./configure --prefix=/usr/local \
                --mandir=/usr/local/man \
                --with-pkgconfigdir=/usr/local/libdata/pkgconfig \
                --enable-lib --enable-util
}

package() {
    cd "${srcdir}/fuse-${pkgver}"

    make DESTDIR="${pkgdir}" install

    install -dm755 "${pkgdir}/usr/local/include/fuse"
    install -m644 include/fuse_kernel.h "${pkgdir}/usr/local/include/fuse/fuse_kernel.h"
}
