# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Wolfgang Bumiller <archbsd at bumiller dot com>

pkgname=liblqr
pkgver=0.4.1
pkgrel=3
pkgdesc="A seam-carving C/C++ library called Liquid Rescale"
arch=('i686' 'x86_64')
url="http://liblqr.wikidot.com/"
license=('GPL')
depends=('glib2')
makedepends=('pkgconf' 'gmake')
options=('!libtool' '!emptydirs')
source=("http://liblqr.wikidot.com/local--files/en:download-page/$pkgname-1-$pkgver.tar.bz2")
md5sums=('0e24ed3c9fcdcb111062640764d7b87a')

build() {
  cd "$srcdir/$pkgname-1-$pkgver"
  sed -i '' -e 's/g++/$(CXX)/' examples/Makefile
  sed -i '' -e '/^lqr_pkgconfigdir/ s|$(libdir)|${exec_prefix}/libdata|' Makefile.in
  sed -i '' -e 's|/usr.*chunk\.xsl|/usr/local/share/xsl/docbook/html/chunk.xsl|' \
  	docs/lqr_style.xsl
  sed -i '' -e 's|/usr.*docbookx.dtd|/usr/local/share/xml/docbook/4.5/docbookx.dtd|' \
  	docs/liblqr_manual.docbook
  CFLAGS="$CFLAGS -std=gnu89" \
  ./configure
  gmake
}

package() {
  cd "$srcdir/$pkgname-1-$pkgver"
  gmake DESTDIR="$pkgdir/" install
}
