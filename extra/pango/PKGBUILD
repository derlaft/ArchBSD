# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pango
pkgver=1.30.1
pkgrel=1
pkgdesc="A library for layout and rendering of text"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('libthai' 'cairo' 'libxft' 'harfbuzz')
makedepends=('harfbuzz' 'fontconfig' 'cairo' 'libxft' 'libthai' 'gobject-introspection')
options=('!libtool')
install=pango.install
source=(http://download.gnome.org/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
url="http://www.pango.org/"
sha256sums=('3a8c061e143c272ddcd5467b3567e970cfbb64d1d1600a8f8e62435556220cbe')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr/local --sysconfdir=/usr/local/etc \
      --localstatedir=/var --with-included-modules=basic-fc \
      --enable-static \
      --with-html-dir=/usr/local/share/doc
  gmake
}

package() {
  cd ${pkgname}-${pkgver}
  gmake -j1 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/local/etc/pango"

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"
}
