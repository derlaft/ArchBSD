# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=weechat
pkgver=0.4.0
pkgrel=1
pkgdesc="Fast, light and extensible IRC client (curses UI)"
arch=('i686' 'x86_64')
url="http://www.weechat.org/"
license=('GPL')
depends=('gnutls' 'curl' 'libgcrypt')
# NOTE: TCL caused some cmake error for me
makedepends=('cmake' 'pkgconf' 'perl' 'python2' 'lua' 'tcl>=8.6.0' 'ruby' 'aspell' 'guile')
optdepends=('perl' 'python2' 'lua' 'tcl' 'ruby' 'aspell' 'guile')
options=('!libtool')
source=("http://www.weechat.org/files/src/${pkgname}-${pkgver}.tar.bz2"
        patch-cmake-FindLua_cmake
        patch-cmake-FindPython_cmake
        patch-cmake-FindRuby_cmake)
md5sums=('b2e10ca1e3a72142fcc8d58c7c774b1b'
         '4da3ce21943edcdaee83e8ef8bdabee8'
         '76592f1e8d9aad6c28fdad58cdac7913'
         'b92f21b005534d383f0329a27bb4cf62')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

# ports patches
  patch -p0 -i "${srcdir}/patch-cmake-FindLua_cmake"
  patch -p0 -i "${srcdir}/patch-cmake-FindPython_cmake"
  patch -p0 -i "${srcdir}/patch-cmake-FindRuby_cmake"

  sed -i '' -e 's|$${LIBDIR}/../pkgconfig|$${CMAKE_INSTALL_PREFIX}/libdata/pkgconfig|' CMakeLists.txt
  sed -i '' -e 's|INSTALL|#INSTALL|' doc/CMakeLists.txt

  mkdir build
  cd build
  cmake .. -DPREFIX=/usr/local \
        -DPYTHON_EXECUTABLE=/usr/local/bin/python2 \
        -DPYTHON_LIBRARY=/usr/local/lib/libpython2.7.so \
        -DENABLE_TCL=yes
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/local/man/man1"
  install -m644 "${srcdir}/${pkgname}-${pkgver}/doc/weechat-curses.1" "${pkgdir}/usr/local/man/man1/weechat-curses.1"
}
