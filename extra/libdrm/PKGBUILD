# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdrm
pkgver=2.4.41
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('libpciaccess')
makedepends=('cairo')
replaces=('libdrm-new' 'libdrm-nouveau')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/$pkgname/$pkgname-$pkgver.tar.bz2
        no-pthread-stubs.patch
        COPYING
        buildfix.diff
        extra-xf86drmMode.c
        )
sha256sums=('52b3ca670df43fc9f5164f0f55fa40b008de297c0ceedab7c77c30bf9022f212'
            '3915aaff4d5b0ed523c3144911e7f79ee812dece0e3933c13a88dddcf5912ded'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93'
            '9b293bbe682381b6822b5abfb1b1ce713185bcefde255d300367b26f47102ddc'
            '87ee4bf81818dc8e1929f1d640ed3002b5cff1f1b458247801060866f6ec4e4a')

build() {
  cd $pkgname-$pkgver

  patch -p0 -i "${srcdir}/extra-xf86drmMode.c"

  patch -Np1 -i ../no-pthread-stubs.patch

  # man/Makefile,in is missing
  patch -Np1 -i ${srcdir}/buildfix.diff

  sed -i '' -e '/86|x86_64) INTEL=yes/ s@x86_64@&|amd64@' configure

  #libtoolize --force
  autoreconf --force --install
  PCIACCESS_CFLAGS="-I/usr/local/include" \
  PCIACCESS_LIBS="-lpciaccess" \
  ./configure --prefix=/usr/local \
      --enable-nouveau-experimental-api
  make
  cd intel
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  make -C intel DESTDIR="$pkgdir" install

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"

  install -m755 -d "$pkgdir/usr/local/share/licenses/$pkgname"
  install -m644 ../COPYING "$pkgdir/usr/local/share/licenses/$pkgname/"
}
