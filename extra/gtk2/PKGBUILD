pkgbase=gtk2
pkgname=('gtk2' 'gtk-update-icon-cache')
pkgver=2.24.14
pkgrel=2
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
makedepends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage'
             'shared-mime-info' 'cairo' 'libcups' 'gdk-pixbuf2' 'gobject-introspection')
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-$pkgver.tar.xz
        xid-collision-debug.patch
	patch-Makefile.in
	patch-ah
	patch-ai
	patch-ak
	patch-docs_reference_gtk_Makefile.in
	patch-gdk-2.0.pc.in
	patch-gtk_gtkbuilderparser.c
	patch-gtk_gtksignal.h
	patch-gtk_updateiconcache.c)

sha256sums=('8bd1b8b511a3004e8972badf467ce829e6855cdff15540b9344dc934c68008ac'
            '88a5f572a78f3f2095e8ad3fca5003103f71b25c8071fd0261ade8c431aaa248'
            '3a090e37a543b71ee7fd37fffbbaeadd212dd0ad9f84ad4f8788488c14dfa2d9'
            '166fe68fae64720d805c6df7e2650884b41d19e051452e28ec74a7ddfd061322'
            '132dda954a38d9796f83505cdf4172b5999775e3593afc1805da8775f7d073c0'
            '025815a15f5541c43b48496003a259748ef446529504ed2e44dbce140bd95540'
            'ccbb0fc0a324e8acd2ead702a784a6e9947e6d0ddcb7e1b8d374ade26249f544'
            '30ee3e7e46d39474ed52f4afdd84132de53f9dea1b0c2b4b27b8a9a380ed7d89'
            '7a2089acc9553f48c73a18d31bd302b82a65b06d7a0a61f72077961ddb4b33ee'
            'b7cd69118b2847d5f5e0b250ad548cccdc579d50826b421c6ee6afedf05ae32c'
            '5e082f9e0109a1a63e713d1997d277251a3f7760efedccd4c9253e5e9aaae8a9')

build() {
    cd gtk+-$pkgver
    patch -Np1 -i ../xid-collision-debug.patch
    patch -p0 -i ${srcdir}/patch-Makefile.in
	patch -p0 -i ${srcdir}/patch-ah
	patch -p0 -i ${srcdir}/patch-ai
	patch -p0 -i ${srcdir}/patch-ak
	patch -p0 -i ${srcdir}/patch-docs_reference_gtk_Makefile.in
	patch -p0 -i ${srcdir}/patch-gdk-2.0.pc.in
	patch -p0 -i ${srcdir}/patch-gtk_gtkbuilderparser.c
	patch -p0 -i ${srcdir}/patch-gtk_gtksignal.h
	patch -p0 -i ${srcdir}/patch-gtk_updateiconcache.c	

    CXX=/usr/bin/false ./configure --prefix=/usr/local \
        --sysconfdir=/usr/local/etc/ \
        --localstatedir=/var \
        --with-xinput=yes

    gmake
}

package_gtk2() {
    pkgdesc="GTK+ is a multi-platform toolkit (v2)"
    install=gtk2.install
    depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'shared-mime-info' 'cairo' 'libcups' 'gtk-update-icon-cache')
    backup=(usr/local/etc/gtk-2.0/gtkrc)
    replaces=('gtk2-docs')

    cd gtk+-$pkgver

    gmake DESTDIR="$pkgdir" install
    sed -i '' \
     -e "s#env python#env python2#" $pkgdir/usr/local/bin/gtk-builder-convert

    echo 'gtk-fallback-icon-theme = "gnome"' > "$pkgdir/usr/local/etc/gtk-2.0/gtkrc"

    #split this out to use with gtk3 too
    rm $pkgdir/usr/local/bin/gtk-update-icon-cache

    install -dm755 "${pkgdir}/usr/local/libdata"
    mv "${pkgdir}/usr/local/lib/pkgconfig" \
       "${pkgdir}/usr/local/libdata/pkgconfig"
}
package_gtk-update-icon-cache() {
    pkgdesc="The GTK+ update icon cache tool"
    depends=('gdk-pixbuf2>=2.24.1-1')

    cd gtk+-$pkgver/gtk

    install -dm755 $pkgdir/usr/local/bin/
    install -m755 gtk-update-icon-cache $pkgdir/usr/local/bin/
}

