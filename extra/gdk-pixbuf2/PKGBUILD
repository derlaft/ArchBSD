# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=gdk-pixbuf2
pkgver=2.26.5
pkgrel=1
pkgdesc="An image loading library"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
depends=('glib2>=2.34.3' 'libpng' 'libtiff' 'libjpeg' 'libx11')
makedepends=('gobject-introspection')
#options=('!libtool')
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-$pkgver.tar.xz)

build() {
  cd gdk-pixbuf-$pkgver

  ./configure --prefix=/usr/local \
    --without-libjasper \
    --with-x11 \
    --with-included-loaders=png
# ltverhack:
  for i in ltmain.sh libtool; do
    sed -i '' -e '/freebsd-elf)/,/;;/ s|major="\.$current"|major=.0|' \
              -e '/freebsd-elf)/,/;;/ s|versuffix="\.$current"|versuffix="$major"|' \
        $i
  done
  gmake
}

package() {
  cd gdk-pixbuf-$pkgver

  gmake DESTDIR="$pkgdir" install

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" \
     "${pkgdir}/usr/local/libdata/pkgconfig"
}

# vim:set ts=2 sw=2 et:
md5sums=('339329e6d619ee3e1cb93979111b04c0')
