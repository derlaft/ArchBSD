# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=atk
pkgver=2.6.0
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility"
arch=(i686 x86_64)
license=('LGPL')
depends=('glib2>=2.34.3')
makedepends=('gobject-introspection')
# NOTE: if you remove !libtool, you need to fix the ltverhack
options=('!libtool' '!docs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
url='http://www.gtk.org/'
sha256sums=('eff663f90847620bb68c9c2cbaaf7f45e2ff44163b9ab3f10d15be763680491f')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr/local --enable-static \
              --with-html-dir=/usr/local/share/doc
              # the glib-test uses variables from the glib-library but doesn -lglib
  gmake
}

package() {
  cd "$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install

  install -dm755 "${pkgdir}/usr/local/libdata"
  mv "${pkgdir}/usr/local/lib/pkgconfig" "${pkgdir}/usr/local/libdata/pkgconfig"

  cd "${pkgdir}/usr/local/lib"
  ln -sf libatk-1.0.so.20609 libatk-1.0.so.10
  ln -sf libatk-1.0.so.20609 libatk-1.0.so.0
  ln -sf libatk-1.0.so.20609 libatk-1.0.so
}
