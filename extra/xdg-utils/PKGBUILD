# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=xdg-utils
_oldver=1.1.0
_gitver=050333e34e2fe7430ad2ead37e5a0420ac0c2a3e # xdg-mime does not search mimeinfo.cache
_gitdate=20121008
pkgver=${_oldver}.git${_gitdate}
pkgrel=2
pkgdesc="Command line tools that assist applications with a variety of desktop integration tasks"
arch=('any')
url="http://portland.freedesktop.org/"
license=('MIT')
depends=('xorg-xset') # xset needed inside xdg-screensaver
makedepends=('docbook-xsl')
optdepends=('kdebase-runtime: for KDE support in xdg-open'
            'libgnome: for GNOME support in xdg-open'
            'exo: for Xfce support in xdg-open'
            'xorg-xprop: for Xfce support in xdg-open'
            'pcmanfm: for LXDE support in xdg-open'
            'perl-file-mimeinfo: for generic support in xdg-open')
source=(#http://cgit.freedesktop.org/xdg/xdg-utils/snapshot/xdg-utils-${_gitver}.tar.gz
        ftp://ftp.archlinux.org/other/$pkgname/xdg-utils-${_gitver}.tar.gz
        patch-scripts_xdg-email
        patch-scripts_xdg-mime
        patch-scripts_xdg-open
        patch-scripts_xdg-screensaver
        )
md5sums=('cc321af15724cf7ee1ef014c4a0120ea'
         '8575c30dde179820a706824043069859'
         'a37794520ae1fc5a72e9a7f6af4295a1'
         '7fe65ec2c3ed27b104a6207c9e122fad'
         'b13c68af22aa77253f81ebd997b910bc')

build() {
	cd ${srcdir}/${pkgname}-${_gitver}
	for i in patch-scripts_xdg-email patch-scripts_xdg-mime patch-scripts_xdg-open patch-scripts_xdg-screensaver
	do
		msg "$i"
		patch -p0 -i "${srcdir}/$i"
	done
	./configure --prefix=/usr/local --mandir=/usr/local/man
#	make scripts-clean -C scripts # needed if it's a git snapshot
	make man scripts -C scripts # needed if it's a git snapshot
	make # for release builds
}

package() {
	cd ${srcdir}/${pkgname}-${_gitver}
	make DESTDIR="${pkgdir}" install
	install -d -m755 "${pkgdir}/usr/local/share/licenses/${pkgname}/"
	install    -m644 LICENSE "${pkgdir}/usr/local/share/licenses/${pkgname}/LICENSE"
	
	# install empty directory to prevent xdg-desktop-menu install *.desktop to fail, see FS#33316
	install -dm755 $pkgdir/usr/local/share/desktop-directories
}
