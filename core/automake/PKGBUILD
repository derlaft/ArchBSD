# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=automake
pkgver=1.13.1
pkgrel=1
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('any')
license=('GPL')
url="http://www.gnu.org/software/automake"
groups=('base-devel')
depends=('perl' 'bash')
makedepends=('autoconf')
install=automake.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        patch-Makefile.in patch-doc__automake.texi
)
md5sums=('a60380ab11e1481376b7747d1b42ced2'
         '272d0de0756252b1835234cca0ff2cfe'
         '52a02ec31484bcdc13c6efb94177954b'
         'c46574b82d37a512e60be15d527dbc9a')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p0 -i "$srcdir"/patch-Makefile.in
  patch -p0 -i "$srcdir"/patch-doc__automake.texi
  ./configure --prefix=/usr/local
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
