# $Id: PKGBUILD 146745 2012-01-16 22:52:52Z dreisner $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libassuan
pkgver=2.0.3
pkgrel=1
pkgdesc="A IPC library used by some GnuPG related software"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnupg.org/related_software/libassuan"
depends=('gettext' 'libiconv' 'libgpg-error')
options=('!libtool')
source=(ftp://ftp.gnupg.org/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2)
install=libassuan.install

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

# ltverhack:
  sed -i '' -e 's/\(LIBASSUAN_LT_CURRENT\)=3/\1=0/' configure
  sed -i '' -e 's/\(LIBASSUAN_LT_REVISION\)=0/\1=3/' configure
  sed -i '' -e 's/\(LIBASSUAN_LT_AGE\)=3/\1=0/' configure

  ./configure --prefix=/usr/local \
              --mandir=/usr/local/man \
              --infodir=/usr/local/info
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
md5sums=('179d1918325fdb928c7bd90b8a514fc7')
