# $Id: PKGBUILD 156350 2012-04-17 15:57:56Z andyrtr $
# Maintainer: judd <jvinet@zeroflux.org>

pkgname=libgpg-error
pkgver=1.10
pkgrel=2
pkgdesc="Support library for libgcrypt"
arch=(i686 x86_64)
url="http://www.gnupg.org"
license=('LGPL')
makedepends=('libtool')
depends=('gettext' 'libiconv')
options=(!libtool)
install=libgpg-error.install
source=(
    ftp://ftp.gnupg.org/gcrypt/libgpg-error/${pkgname}-${pkgver}.tar.bz2)
    #ftp://ftp.franken.de/pub/crypt/mirror/ftp.gnupg.org/gcrypt/libgpg-error/${pkgname}-${pkgver}.tar.bz2
    #http://gnupg.org.favoritelinks.net/libgpg-error/${pkgname}-${pkgver}.tar.bz2)
md5sums=('736a03daa9dc5873047d4eb4a9c22a16')


build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

# ltverhack:
  sed -i '' -e 's/\(LIBGPG_ERROR_LT_CURRENT\)=8/\1=0/' configure
  sed -i '' -e 's/\(LIBGPG_ERROR_LT_AGE\)=8/\1=0/' configure
  sed -i '' -e 's/\(LIBGPG_ERROR_LT_REVISION\)=0/\1=8/' configure

  ./configure --prefix=/usr/local \
              --with-libiconv-prefix=/usr/local \
              --with-libintl-prefix=/usr/local \
              --enable-static --enable-shared
  gmake
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  gmake check
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  gmake DESTDIR="${pkgdir}/" install
}
